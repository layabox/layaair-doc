# 七、主角射击与掉落道具



La plupart des jeux de la guerre des avions sont terminés.Pour un jeu de tir, le tir à balles tue l 'ennemi et le vol d' accessoires est le jeu de base.Ce cours nous permet de réaliser ces deux fonctions essentielles.



###Tirez sur le personnage principal.

Dans notre ancien programme, nous avons placé le tir dans la catégorie principale de main, mais dans la catégorie principale, c 'est la logique principale, les codes sont plus nombreux et le tir est un acte du personnage.

Dans la catégorie role, l 'intervalle de tir supplémentaire est la propriété shootinval et l' intervalle de tir suivant, et le rôle est - il la propriété isbullet du type de balle?Un nouveau procédé est ensuite créé.

Dans le procédé de tir, une balle est créée à intervalles de tir, l 'objet étant lui aussi l' objet du personnage, la différence étant qu 'il n' a que l 'état de vol et qu' il n 'y a pas de lésions mortelles.

####Procédé de création de tir


```

		......
		/***射击间隔***/
		public var shootInterval: Number= 300;
		/***下次射击时间***/
		public var shootTime: Number= 300;
		......
		/**
		 角色射击，生成子弹
		 */		
		public function shoot():void
		{
			//获取当前时间
			var time:Number = Browser.now() ;
			//如果当前时间大于下次射击时间
			if (time >this.shootTime)
			{
				//更新下次子弹射击的时间
				this.shootTime = time + this.shootInterval ; 
				//从对象池里面创建一颗子弹
				var bullet: Role = Pool.getItemByClass("role",Role);
				//初始化子弹信息
				//子弹阵营与发射者相同
				bullet.init("bullet2",1,-10,1,this.camp)
				//角色类型为子弹类型
				bullet.isBullet = true;
				//对象池中对象死亡时会被隐藏，重新显示
				bullet.visible=true;
				//设置子弹发射初始化位置
				bullet.pos(this.x, this.y-80);
				//添加到角色层中
				this.parent.addChild(bullet);
			}
		}
```




####Modification de la méthode d 'hémorragie

Étant donné que les balles n'étaient pas animées par la mort et qu'elles avaient été abattues sur l'ennemi, il fallait augmenter les points de vue des joueurs.Ainsi, le code dans le procédé losthp () est modifié.

/ * *
- hémorragie.
*
Public function losthp (losthp: Number): void
{C:}
Réduction du sang
This.hp - = losthp;
/ / en fonction du volume sanguin
F (this.hp > 0)
{C:}
/ / si elle n 'est pas morte, l' animation est projetée.
Playation ("hit");
- autre chose.
{C:}
/ / en cas de décès, une animation explosive est projetée.
Les balles ne sont pas mortes.
F (this.isbullet)
{C:}
Dans le procédé /
This.visible = false;
- autre chose.
{C:}
Ajouter l 'animation de la mort
This.playation ("Die");
Si vous tombez sur le sang, la victime n'est pas un personnage ou une balle.
If (this.type! = "Hero" & this.isbulletin)
{C:}
/ / Augmenter l 'intégration du jeu
Main.score +;
- Oui.
- Oui.
- Oui.
- Oui.
Une fois ce code terminé, il faut veiller particulièrement à ce que de nouveaux ajouts soient apportés à la méthode d 'initialisation de personnages init ().**Isbullet = False**Dans le cas contraire, les objets créés à partir de la piscine d 'objets peuvent ne pas reproduire l' animation de la mort, leur isbullet pouvant être vrai.

####Modifier le cycle principal

Ajouter un procédé de tir d 'acteur principal dans le Loop principal de la classe main et modifier le Code de manière à ne pas tirer après la mort du personnage.


```

		/**
		 游戏主循环
		 */
		private function loop():void
		{
			//本局游戏数据更新
			play.update(hero.hp,level,score)
			//如果主角死亡
			if(hero.hp<=0)
			{
				//玩家飞机死亡后延迟时间，100帧后弹出游戏结束界面
				this.deathTime++
				if (this.deathTime>=100)
				{
					this.deathTime=0;
					//游戏结束
					gameOver();
					//本方法内后续逻辑不执行
					return;
				}
			}else
			{
				//主角未死亡将持续射击
				hero.shoot();
			}
		    ......
```


Tous les codes ont été modifiés et compilés pour voir la balle créée et tirée!L'ennemi a été blessé ou détruit lorsque la balle a heurté l'ennemi.Les parts cumulées dans l'ui augmenteront également (fig. 1).

![思维导图.png](img/1.png)< br / > (Figure 1)

#### 

###Les jeux vidéo tombent et les mangent.

Une fois qu'une bombe a explosé, on peut mettre en place un moyen d'abattre l'ennemi.Il y a deux sortes d 'instruments dans nos ressources artistiques, l' un étant un niveau de balles et, lorsque le niveau de balles est suffisant, plusieurs balles sont tirées simultanément; l 'autre est conçu pour augmenter le volume sanguin.

####Procédé de création d 'un outil

Le procédé de création d 'un lostprop () dans la catégorie role, seuls les avions ennemis du type enemy3 peuvent tomber sur l' appareil, qui est également une catégorie de personnages et produit l 'appareil de la même manière que les autres acteurs.


```

		
		/**角色死亡掉落物品**/
		private function lostProp():void
		{
			//只有boss才能掉落道具
			if(this.type!="enemy3") return;
			//从对象池里面创建一个道具
			var prop:Role =Pool.getItemByClass("role",Role);
			
			//生成随机道具类型
			var r:Number=Math.random();
			var num:int=(r<0.7)?1:2;
			
			//重新初始化道具属性,阵营为敌方（能与主角发生碰撞）
			prop.init("ufo"+num,1,2,30,1);
			//强制显示
			prop.visible=true;
			//生成的位置为死亡者位置
			prop.pos(this.x,this.y);
			//加载到父容器
			this.parent.addChild(prop);
		}
```


####Modifier le procédé de retour de l 'animation terminée

Lorsque le personnage a terminé la lecture de l 'animation de la mort, on peut appeler la méthode lostprop () et donc modifier la méthode oncomplete () dans la classe role.

/ ***Procédé de retour après Animation***
Private Function oncomplete (): void
{C:}
Si le rôle n 'est pas large, obtenez la hauteur
F (roleani.with = = 0)
{C:}
/ / acquisition de limites rectangulaires d 'animation
Var = Bounds: Rectangle = roleani.getbounds ();
/ / attribution de la largeur de rôle
Roleani.size (bounds.with, bounds.height)
- Oui.
Si l 'animation de la mort est terminée
F (this.action = = "Die")
{C:}
Dans le procédé /
This.visible = false;
/ / instruments tombés après la mort
This.lostprop ();
- Oui.
...


On va abattre un avion de l'ennemi boss.Mais nous avons également trouvé le problème, car les instruments sont différents et sont donc susceptibles d'être détruits par des balles, tandis que les protagonistes et les appareils tombent entre eux en collision.

![思维导图.png](img/1.png)<br />（图1）







####Procédé de création de personnages

Pour résoudre les problèmes qui se posent, les balles peuvent être améliorées après avoir mangé des munitions.

Nous devons ajouter à la catégorie role de nouveaux types d'instruments, de niveaux de balles, de nombre de balles tirées simultanément et de caractéristiques de déplacement de position des balles.

Etablir un procédé d 'eatprop (), puis faire un diagnostic de collision entre un avion et un appareil dans le cycle principal de la classe main.


```

		/***道具类型 0:飞机或子弹，1:子弹箱，2:血瓶***/
		public var propType:int=0;
		/***子弹级别（吃子弹道具后升级）***/
		public var bulletLevel: Number = 0;
		/***同时射击子弹数量***/
		public var shootNum: Number= 1;
		/***子弹偏移的位置***/
		private var bulletPos: Array = [[0], [-15, 15], [-30, 0, 30], [-45, -15, 15, 45]];
		......
		
		/**
		 * 角色吃到道具，加血或子弹
		 */		
		public function eatProp(prop:Role):void
		{
			//如果调用者不是主角或prop不是道具，则返回
			if(this.type!="hero"||prop.propType==0) return;
				
			//吃子弹箱
			if(prop.propType==1) 
			{
				//积分增加
				Main.score++;
				//子弹级别增加
				this.bulletLevel++
                //子弹每升2级，子弹数量增加1，最大数量限制在4个
                this.shootNum = Math.min(Math.floor(this.bulletLevel / 2) + 1,4);
				//子弹级别越高，发射频率越快
				this.shootInterval = 300 - 8 * (this.bulletLevel > 8 ? 8 : this.bulletLevel);
			}
			else if(prop.propType==2)//吃血
			{
				//血量增加
				this.hp+=2;
                //积分增加
				Main.score+=1;
			}
			//道具死亡
			prop.hp=0；
			//道具吃完后消失，下一帧回收
			prop.visible=false;
		}
			......
```


Lors de la création de l 'accessoire, ajouter les attributs de l' accessoire proptype et modifier le procédé lostprop ().


```

		/**角色死亡掉落物品**/
		private function lostProp():void
		{
			if(this.type!="enemy2") return;
			//从对象池里面创建一个道具
			var prop:Role =Pool.getItemByClass("role",Role);
			
			//生成随机道具类型
			var r:Number=Math.random();
			var num:int=(r<0.7)?1:2;
			
			//重新初始化道具属性,阵营为敌方（只与主角发生碰撞）
			prop.init("ufo"+num,1,2,30,1);
			//道具类型
			prop.propType=num;
			......
```


Modifie la méthode role type init () et utilise les attributs de dosage 0 pour l 'initialisation.


```

		/**
		 * 角色初始化
		 * @param type  角色类型：“hero”:玩家飞机，“enemy1-3”：敌人飞机、“bulle:1-2”：子弹、"ufo1-2":道具
		 * @param hp      血量
		 * @param speed   速度
		 * @param hitRadius   碰撞半径
		 * @param camp    阵营
		 */		
		public function init(type:String,hp:Number,speed:Number,hitRadius:Number,camp:Number):void
		{
			......
			//对象基本都从对象池中创建，如果之前为子弹，不重新赋值的话不会播放死亡动画
			this.isBullet=false;
			
			//道具属性初始为0
			this.propType=0;
			......
```


Après avoir modifié le code ci - dessus, nous pourrons évaluer l 'impact dans le groupe principal main.



####Diagnostics of Role and Tool collision

Modifier le Code de collision dans le cycle principal de la classe main, puis le faire fonctionner, nous avons trouvé que nous pouvons manger des accessoires.


```

			//游戏碰撞逻辑
			//遍历所有飞机，更改飞机状态
			for (var i: int = roleLayer.numChildren - 1; i > -1; i--) 
			{
				//获取第一个角色
				var role:Role = roleLayer.getChildAt(i) as Role;
				//角色自身更新
				role.update();				
				//如果角色死亡，下一循环
				if(role.hp<=0) continue;
				//碰撞检测
				for(var j:int=i-1;j>-1;j--)
				{	//获取第二个角色
					var role1:Role=roleLayer.getChildAt(j) as Role;
					//如果role1未死亡且不同阵营
					if(role1.hp>0&&role1.camp!=role.camp)
					{
						//获取碰撞半径
						var hitRadius:int=role.hitRadius+role1.hitRadius;
						//碰撞检测
						if(Math.abs(role.x-role1.x)<hitRadius&&Math.abs(role.y-role1.y)<hitRadius)
						{
							//如果某一个碰撞体是道具，则吃道具，否则掉血
							if(role.propType!=0||role1.propType!=0)
							{
								//无法判断哪个是道具，因此都相互吃吃试一试
								role.eatProp(role1);
								role1.eatProp(role);
							}else
							{
								//角色相互掉血
								role.lostHp(1);
								role1.lostHp(1);
							}
					}
				}
			}
```




####Les balles se sont améliorées après avoir mangé des accessoires.

Modification de la méthode de tir des balles dans la catégorie Role () et augmentation du nombre de balles tirées lorsque le niveau de tir augmente après avoir mangé le matériel


```

		/**
		 角色射击，生成子弹
		 */		
		public function shoot():void
		{
			//获取当前时间
			var time:Number = Browser.now() ;
			//如果当前时间大于下次射击时间
			if (time >this.shootTime)
			{
				//获得发射子弹的位置数组
				var pos:Array=bulletPos[this.shootNum-1]
				for(var i:int=0;i<pos.length;i++)
				{
					//更新下次子弹射击的时间
					this.shootTime = time + this.shootInterval ; 
					//从对象池里面创建一个子弹
					var bullet: Role = Pool.getItemByClass("role",Role);
					//初始化子弹信息
					bullet.init("bullet2",1,-10,1,this.camp)
					//角色类型为子弹类型
					bullet.isBullet = true;
					//子弹消失后会不显示，重新显示
					bullet.visible=true;
					//设置子弹发射初始化位置
					bullet.pos(this.x+pos[i], this.y-80);
					//添加到角色层
					this.parent.addChild(bullet);
				}
			}
		}
```


Après avoir mangé deux ou trois cartouches, le niveau de la balle a augmenté, mais aussi plusieurs balles peuvent être tirées simultanément (fig. 2) et la force d 'attaque est devenue catastrophique!

![思维导图.png](img/2.png)< br / > (Figure 2)



###Attention à l 'utilisation d' un réservoir d 'objets

Un mécanisme de création et de récupération de pool de bassin d 'objets est indispensable pour développer le jeu avec layaair, ce qui permet de réduire les coûts de création d' objets.En particulier, des jeux de tir comme la guerre aérienne ont permis de créer rapidement un grand nombre de cartouches, d'avions et de détruire rapidement, ce qui rend encore plus nécessaire l'utilisation d'un bassin cible.

Pour les débutants, l'ignorance de certains problèmes peut être source de problèmes de procédure et il est difficile d'essayer de trouver des solutions.En ce qui concerne l'utilisation, il convient de noter ce qui suit:

Le réservoir d'objets est une référence pour l'objet récupéré.Si l 'un de vos exemples globaux est récupéré, comme Hero, le héros de la guerre aérienne, il risque d' être utilisé par de nouveaux sujets, peut - être une balle, un avion ennemi.Si ses propriétés sont modifiées, le personnage principal Hero sera également modifié.Il est donc recommandé que l 'objet global ne soit pas récupéré dans le bassin de l' objet.

Les attributs doivent être réinitialisés en fonction de l'objet créé par la poêle d'objets.Par exemple, les avions ennemis et les balles pendant la guerre aérienne, dont la visibilité était false au moment de la mort, et les balles imboulette pour la vérité, mais dont le rôle était de False.Lorsque vous créez de nouveaux objets, les attributs modifiés doivent être initialisés et, si vous oubliez les modifications, des problèmes surgissent dans certaines de leurs parties logiques.

Par exemple, pour déterminer si un personnage projette une animation de la mort, on utilise isbullet pour déterminer si, dans le cas d'un avion ennemi, l'animation de la mort doit être diffusée.Mais s' il s' agit d 'un avion hostile créé à l' aide d 'un objet de récupération de balles, si isbullet est vrai à ce moment - là et si isbullet n' est pas initialisé, il est considéré à tort comme une balle et ne projette pas l 'animation de la mort.

Bien entendu, il y a un autre moyen de créer une classification d'identification lors de la récupération, de classer les balles et les avions, d'ajouter quelques lignes de code et d'éviter les failles logiques.



Nous présentons ci - dessous tous les codes des catégories main et role modifiés.

###Main.as all Code


```

 package {
	
	import laya.display.Sprite;
	import laya.display.Stage;
	import laya.events.Event;
	import laya.net.Loader;
	import laya.utils.Handler;
	import laya.utils.Pool;
	import laya.utils.Stat;
	import laya.webgl.WebGL;
	
	
	public class Main
	{
		/**开始页面***/
		private var start:GameStart
		/**地图页面***/
		private var map:GameMap
		/**游戏中界面***/
		private var play:GamePlay
		/**游戏结束页面***/
		private var over:GameOver
		
		/**游戏关卡数***/
		public static var level:int=1;
		/**玩家得分***/
		public static var score:int=0;
		
		/**角色层容器***/
		private var roleLayer:Sprite;
		/**玩家主角***/
		private var hero:Role;
		
		
		/**鼠标上一帧x座标** */		
		private var moveX:Number;
		/**鼠标上一帧y座标** */	
		private var moveY:Number;
		
		
		/****敌机血量表****/
		private var hps: Array = [1, 7, 15];
		/***敌机生成数量表**/
		private var nums:Array = [2, 1, 1];
		/***敌机速度表***/
		private var speeds: Array = [3, 2, 1];
		/***敌机被击半径表***/
		private var radius: Array = [20, 35, 80];
		
		/****主角死亡后游戏结束时间***/
		private var deathTime:int=0
		
		
		public function Main()
		{
			//初始化引擎，建议增加WebGl模式
			Laya.init(720, 1280,WebGL);
			
			//全屏不等比缩放模式
			Laya.stage.scaleMode = Stage.SCALE_EXACTFIT;
			//加载游戏页面资源(如果界面资源太多太大[超过50k],建议开始页面单独建立文件夹打包)
			Laya.loader.load("res/atlas/gameUI.atlas",Handler.create(this,this.gameStart))
				
		}
		
		/**
		 资源加载完成后，加载游戏开始界面
		 */
		private function gameStart():void
		{
			//实例化开始页面
			start=new GameStart();
			//以弹出方式打开，有缓动效果。IDE中页面为Dialog才可用
			start.popup();
			//监听开始游戏开始按钮事件,点击后进入游戏中
			start.btn_start.on(Event.MOUSE_UP,this,gameInit)
		}
		
		/**
		 游戏中，游戏初始化
		 */
		private function gameInit():void
		{
			//缓动动画关闭效果。IDE中页面为Dialog才可用
			start.close();
			
			//实例化地图背景页面(如果已实例化，不需要重新new)
			map||=new GameMap();
			//加载到舞台
			Laya.stage.addChild(map);
			
			
			//实例化角色层并加载到舞台(如果已实例化，不需要重新new)
			roleLayer||=new Sprite();
			Laya.stage.addChild(roleLayer);
			
			//实例化游戏中UI页面(如果已实例化，不需要重新new)
			play||=new GamePlay();			
			//加载到舞台
			Laya.stage.addChild(play);
			
			//实例化主角(如果已实例化，不需要重新new)
			hero||=new Role();
			//初始化角色类型、血量，注：速度speed为0，因为主角是通过操控改变位置,阵营为0
			hero.init("hero",10,0,30,0);
			//死亡后会隐藏，重新开始后需显示
			hero.visible=true;
			//主角位置修改
			hero.pos(360,800);
			//角色加载到角色层中
			roleLayer.addChild(hero);
			
			//鼠标按下监听
			Laya.stage.on(Event.MOUSE_DOWN,this,onMouseDown);
			//鼠标抬起监听
			Laya.stage.on(Event.MOUSE_UP,this,onMouseUp);
			
			
			//模拟游戏结束，30秒时间
//			Laya.timer.once(30000,this,gameOver);
			//游戏主循环
			Laya.timer.frameLoop(1,this,loop);
		}
		
		/**
		 点击开始触发移动
		 */	
		private function onMouseDown():void
		{
			//记录鼠标按下时的位置，用于计算鼠标移动量
			moveX=Laya.stage.mouseX;
			moveY=Laya.stage.mouseY;
			//
			Laya.stage.on(Event.MOUSE_MOVE,this,onMouseMove);
		}
		
		/**
		 主角跟随鼠标移动
		 */	
		private function onMouseMove():void
		{
			//计算角色移动量
			var xx:Number=moveX-Laya.stage.mouseX;
			var yy:Number=moveY-Laya.stage.mouseY;
			//更新移动位置
			hero.x-=xx;
			hero.y-=yy;
			//更新本帧的移动座标
			moveX=Laya.stage.mouseX;
			moveY=Laya.stage.mouseY;
		}
		/**
		 鼠标抬起、关闭移动监听
		 */		
		private function onMouseUp():void
		{
			Laya.stage.off(Event.MOUSE_MOVE,this,onMouseMove) ;
		}
		
		
		/**
		 游戏主循环
		 */
		private function loop():void
		{
			//本局游戏数据更新
			play.update(hero.hp,level,score)
			//如果主角死亡
			if(hero.hp<=0)
			{
				//玩家飞机死亡后延迟时间，100帧后弹出游戏结束界面
				this.deathTime++
				if (this.deathTime>=100)
				{
					this.deathTime=0;
					//游戏结束
					gameOver();
					//本方法内后续逻辑不执行
					return;
				}
			}else
			{
				//主角未死亡将持续射击
				hero.shoot();
			}

			//地图滚动更新
			map.updateMap()
				
			//游戏碰撞逻辑
			//遍历所有飞机，更改飞机状态
			for (var i: int = roleLayer.numChildren - 1; i > -1; i--) 
			{
				//获取第一个角色
				var role:Role = roleLayer.getChildAt(i) as Role;
				//角色自身更新
				role.update();				
				//如果角色死亡，下一循环
				if(role.hp<=0) continue;
				//碰撞检测
				for(var j:int=i-1;j>-1;j--)
				{	//获取第二个角色
					var role1:Role=roleLayer.getChildAt(j) as Role;
					//如果role1未死亡且不同阵营
					if(role1.hp>0&&role1.camp!=role.camp)
					{
						//获取碰撞半径
						var hitRadius:int=role.hitRadius+role1.hitRadius;
						//碰撞检测
						if(Math.abs(role.x-role1.x)<hitRadius&&Math.abs(role.y-role1.y)<hitRadius)
						{
							//如果某一个碰撞体是道具，则吃道具，否则掉血
							if(role.propType!=0||role1.propType!=0)
							{
								//无法判断哪个是道具，因此都相互吃试试
								role.eatProp(role1);
								role1.eatProp(role);
							}else
							{
								//角色相互掉血
								role.lostHp(1);
								role1.lostHp(1);
							}
						}
					}
				}
			}
			
			//创建敌机,不同类型飞机创建的间隔时间不一样
			//生成小敌机
			if (Laya.timer.currFrame % 80== 0)
			{
				createEnemy(0, hps[0],speeds[0], nums[0]);
			}
			//生成中型敌机
			if (Laya.timer.currFrame % 160== 0) 
			{
				createEnemy(1 , hps[1],speeds[1], nums[1]);
			}
			//生成boss敌机
			if (Laya.timer.currFrame % 1000== 0) 
			{
				createEnemy(2, hps[2],speeds[2], nums[2]);
			}

		}

		/**
		 *  创建敌人
		 * @param index 	敌人编号
		 * @param hp   		 敌人血量
		 * @param speed		敌人速度
		 * @param num		敌人数量
		 */
		private function createEnemy(index:Number,hp:Number,speed:Number,num:Number):void 
		{
			for (var i: int = 0; i < num; i++)
			{
				//创建敌人，从对象池创建
				var enemy:Role = Pool.getItemByClass("role", Role);
				//初始化敌人
				enemy.init("enemy" + (index+1), hp, speed,radius[index],1);
				//从对象池中创建的对象死亡前被隐藏了，因此要重新初始化显示，否则新创建角色不会显示出来
				enemy.visible=true;
				//随机位置
				enemy.pos(Math.random() *(720-80)+50, -Math.random() * 100);
				//添加到舞台上
				roleLayer.addChild(enemy);
			}
		}
		
		/**
		 游戏结束
		 */
		private function gameOver():void
		{
			//移除所有舞台事件，鼠标操控
			Laya.stage.offAll();
			
			//移除地图背景
			map.removeSelf();
			//移除游戏中UI
			play.removeSelf();
			
			//清空角色层子对象
			roleLayer.removeChildren(0,roleLayer.numChildren-1);
			//移除角色层
			roleLayer.removeSelf();
			
			//去除游戏主循环
			Laya.timer.clear(this,loop);
			
			//实例化游戏结束页面
			over||=new GameOver();
			//游戏积分显示
			over.txt_score.text=score.toString();
			//以弹出方式打开，有缓动效果。IDE中页面为Dialog才可用
			over.popup();
			//重新开始事件监听,点击后进入游戏中
			over.on("reStart",this,gameInit);
		}
	}
}
```




###Code complet de la classe role:


```

package
{
	import laya.display.Animation;
	import laya.display.Sprite;
	import laya.events.Event;
	import laya.maths.Rectangle;
	import laya.utils.Browser;
	import laya.utils.Handler;
	import laya.utils.Pool;
	
	/**
	 * @author CHENZHENG
	 * 角色类，飞机、敌人、子弹、道具
	 */	
	public class Role extends Sprite
	{
		/***飞机的类型   “hero”:玩家飞机，“enemy”：敌人飞机、“bulle”：子弹、"ufo":道具****/
		public var type:String;
		/***飞机的血量***/
		public var hp:Number=0; 
		/***飞机的速度***/
		private var speed:Number=0;	
		
		/***飞机的被攻击半径***/
		public var hitRadius:Number;
		/***飞机的阵营（敌我区别）***/
		public var camp:Number;
		
		/***角色的动画资源***/
		private var roleAni:Animation;
		/***当前动画动作***/
		private var action:String;
		
		/***射击间隔***/
		public var shootInterval: Number= 300;
		/***下次射击时间***/
		public var shootTime: Number= 300;
		/***是否是子弹***/
		public var isBullet:Boolean = false;
		
		/****道具类型 0:飞机或子弹，1:子弹箱，2:血瓶***/
		public var propType:int=0;
		/***子弹级别（吃子弹道具后升级）***/
		public var bulletLevel: Number = 0;
		/***同时射击子弹数量***/
		public var shootNum: Number= 1;
		/***子弹偏移的位置***/
		private var bulletPos: Array = [[0], [-15, 15], [-30, 0, 30], [-45, -15, 15, 45]];
		
		
		public function Role()
		{
			//实例化动画
			roleAni=new Animation();
			//加载IDE编辑的动画文件
			roleAni.loadAnimation("GameRole.ani");
		}
		
		/**
		 * 角色初始化
		 * @param type  角色类型 ---“hero”:玩家飞机，“enemy1-3”：敌人飞机、“bulle:1-2”：子弹、"ufo1-2":道具
		 * @param hp      血量
		 * @param speed   速度
		 * @param hitRadius   碰撞半径
		 * @param camp    阵营
		 */		
		public function init(type:String,hp:Number,speed:Number,hitRadius:Number,camp:Number):void
		{
			//角色初始化属性
			this.type=type;
			this.hp=hp;
			this.speed=speed;
			this.hitRadius=hitRadius;
			this.camp=camp;
			
			//对象基本都从对象池中创建，如果之前为子弹，不重新赋值的话不会播放死亡动画
			this.isBullet=false;
			//道具属性初始为0
			this.propType=0;
			
			//加载动画对象
			this.addChild(roleAni)
			
			//监听动画完成事件
			roleAni.on(Event.COMPLETE,this,onComplete)
			//播放默认飞行动画
			playAction("fly");
		}
		
		/***动画完成后回调方法***/
		private function onComplete():void
		{
			//如果角色还未有宽，获得角色宽高	
			if(roleAni.width==0)
			{
				//获得动画矩形边界
				var bounds:Rectangle=roleAni.getBounds();
				//角色 宽高赋值
				roleAni.size(bounds.width,bounds.height)
			}
			//如果死亡动画播放完成
			if(this.action=="die")
			{
				//update()方法中，隐藏后进行回收
				this.visible=false;
				this.lostProp();
			}
			else if(this.action=="hit")//如果是受伤动画，下一帧播放飞行动画
			{
				this.playAction("fly");
			}
		}
		
		/**
		 * 角色失血
		 */		
		public function lostHp(lostHp:Number):void 
		{
			//减血
			this.hp -= lostHp;
			//根据血量判断
			if (this.hp > 0) 
			{
				//如果未死亡，则播放受击动画
				this.playAction("hit");
			}else 
			{
				//如果死亡，则播放爆炸动画
				if (this.isBullet) 
				{
					//隐藏，下一帧回收
					this.visible=false;
				}else 
				{
					//添加死亡动画
					this.playAction("die");
					//如果碰撞掉血死亡者不是角色和子弹
					if(this.type!="hero"&&!this.isBullet)
					{
						//增加游戏积分
						Main.score++;
					}
				}
			}
		}
		
		/**角色死亡掉落物品**/
		private function lostProp():void
		{
			if(this.type!="enemy3") return;
			//从对象池里面创建一个道具
			var prop:Role =Pool.getItemByClass("role",Role);
			
			//生成随机道具类型
			var r:Number=Math.random();
			var num:int=(r<0.7)?1:2;
			
			//重新初始化道具属性,阵营为敌方（只与主角发生碰撞）
			prop.init("ufo"+num,1,2,30,1);
			//道具类型
			prop.propType=num;
			
			//强制显示
			prop.visible=true;
			//生成的位置为死亡者位置
			prop.pos(this.x,this.y);
			//加载到父容器 
			this.parent.addChild(prop);
		}
		/**
		 * 角色吃到道具，加血或子弹
		 */		
		public function eatProp(prop:Role):void
		{
			//如果调用者不是主角或prop不是道具，则返回
			if(this.type!="hero"||prop.propType==0) return;
			
			//添加吃强化道具音效					
			//吃子弹箱
			if(prop.propType==1) 
			{
				//积分增加
				Main.score++;
				//子弹级别增加
				this.bulletLevel++
				//子弹每升2级，子弹数量增加1，最大数量限制在4个
				this.shootNum = Math.min(Math.floor(this.bulletLevel / 2) + 1,4);
				//子弹级别越高，发射频率越快
				this.shootInterval = 300 - 8 * (this.bulletLevel > 8 ? 8 : this.bulletLevel);
			}
			else if(prop.propType==2)//吃血
			{
				//血量增加
				this.hp+=2;
				//积分增加
				Main.score+=1;
			}
			//道具死亡
			prop.hp=0;
			//道具吃完后消失，下一帧回收
			prop.visible=false;
		}
		
		/**
		 * 播放动画 
		 * @param action 动画状态   "fly"、"hit"、"die"
		 */	
		public function playAction(action:String):void
		{
			this.action=action;
			//播放角色动画,name=角色类型_动画状态，如：hero_fly
			roleAni.play(0,true,this.type+"_"+action);
		} 
		
		/**
		 * 角色更新,边界检查
		 */		
		public function update():void
		{
			//如果角色隐藏，角色消亡并回收
			if(!this.visible)
			{
				//主角不死亡回收，只隐藏，以免其他对象以主角回对象创建，发生引用修改
				if(this.type!="hero") 	this.die();
				return;
			}
			//角色根据速度飞行
			this.y += this.speed;
			
			//如果移动到显示区域以外，则移除
			if (this.type!="hero"&&(this.y > 1280+100||this.y<-150))
			{
				this.visible=false;
			}
			
			//主角边界检查
			if(this.type=="hero")
			{
				//需减去角色宽或高的一半，因为在IDE中制作动画时，我们把角色的中心做为了角色对象的原点
				//判断是否左右超出
				if(this.x<roleAni.width/2)
				{
					this.x=roleAni.width/2;
				}
				else if(this.x>720-roleAni.width/2)
				{
					this.x=720-roleAni.width/2;
				}
				//判断是否上下超出
				if(this.y<roleAni.height/2)
				{
					this.y=roleAni.height/2;
				}
				else if(this.y>1280-roleAni.height/2)
				{
					this.y=1280-roleAni.height/2;
				}
			}
		}

		/**
		 角色射击，生成子弹
		 */		
		public function shoot():void
		{
			//获取当前时间
			var time:Number = Browser.now() ;
			//如果当前时间大于下次射击时间
			if (time >this.shootTime)
			{
				//获得发射子弹的位置数组
				var pos:Array=bulletPos[this.shootNum-1]
				for(var i:int=0;i<pos.length;i++)
				{
					//更新下次子弹射击的时间
					this.shootTime = time + this.shootInterval ; 
					//从对象池里面创建一个子弹
					var bullet: Role = Pool.getItemByClass("role",Role);
					//初始化子弹信息
					bullet.init("bullet2",1,-10,1,this.camp)
					//角色类型为子弹类型
					bullet.isBullet = true;
					//子弹消失后会不显示，重新初始化
					bullet.visible=true;
					//设置子弹发射初始化位置
					bullet.pos(this.x+pos[i], this.y-80);
					//添加到角色层
					this.parent.addChild(bullet);
				}
			}
		}
		
		/**角色死亡并回收到对象池**/
		public function die():void
		{
			//角色动画停止
			this.roleAni.stop(); 
			//去除所有动画监听
			this.roleAni.offAll();
			//从舞台移除
			this.removeSelf();
			//回收到对象池
			Pool.recover("role", this);
		}
	}
}
```
